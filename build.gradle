plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.46,6.2)'
}

group = project.group
description = project.displayName
base.archivesName = project.name
version = '1.21.11-1.12.3'

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'official', version: '1.21.11'

    reobf = false
    copyIdeResources = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.console.level', 'debug'
            property 'eventbus.api.strictRuntimeChecks', 'true'

            arg '-mixin.config=presencefootsteps.mixin.json'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'Forge'
        url = 'https://maven.minecraftforge.net'
    }
    maven {
        name = 'Minecraft libraries'
        url = 'https://libraries.minecraft.net'
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'Sponge'
                url = 'https://repo.spongepowered.org/repository/maven-public'
            }
        }
        filter {
            includeGroupAndSubgroups('org.spongepowered')
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.21.11-61.0.8"
    annotationProcessor 'net.minecraftforge:eventbus-validator:7.0.1'
}

tasks.named('jar', Jar) {
    manifest {
        attributes(['MixinConfigs': 'presencefootsteps.mixin.json'])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

idea.module { downloadJavadoc = downloadSources = true }

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}
